// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
/**
 * The Hack Central Processing unit (CPU).
 * Parses the binary code in the instruction input and executes it according to the
 * Hack machine language specification. In the case of a C-instruction, computes the
 * function specified by the instruction. If the instruction specifies to read a memory
 * value, the inM input is expected to contain this value. If the instruction specifies
 * to write a value to the memory, sets the outM output to this value, sets the addressM
 * output to the target address, and asserts the writeM output (when writeM = 0, any
 * value may appear in outM).
 * If the reset input is 0, computes the address of the next instruction and sets the
 * pc output to that value. If the reset input is 1, sets pc to 0.
 * Note: The outM and writeM outputs are combinational: they are affected by the
 * instruction's execution during the current cycle. The addressM and pc outputs are
 * clocked: although they are affected by the instruction's execution, they commit to
 * their new values only in the next cycle.
 */
CHIP CPU {

    IN  inM[16],          // M = RAM[A]
        instruction[16],  // Current instruction
        reset;            // Reset signal

    OUT outM[16],         // Value to write to RAM
        writeM,           // Write enable for RAM
        addressM[15],     // RAM address (A register)
        pc[15];           // Program counter

    
    PARTS:
      Or(a=instruction[15] , b=false , out=isCinstruction );
      Not(in=instruction[15] , out=isAinstruction );  
    

    Mux16(a=instruction , b=aluOut , sel=instruction[15] , out=inAReg );
    And(a=isCinstruction , b=instruction[5] , out=CDestA );
    Or(a=CDestA , b=isAinstruction , out=loadA );
    ARegister(in=inAReg , load=loadA , out=ARegOut,out[0..14]=addressM );

    And(a=isCinstruction, b=instruction[4], out=loadD); 
    DRegister(in=aluOut , load=loadD , out=xIn );
    Mux16(a=ARegOut , b=inM , sel=instruction[12] , out=yIN );
    ALU(x=xIn , y=yIN ,
     zx=instruction[11] ,
     nx=instruction[10] , 
     zy=instruction[9] , 
     ny=instruction[8] , 
     f=instruction[7] , 
     no=instruction[6] , 
     out=aluOut,out=outM , zr=zr , ng=ng );
    Not(in=zr , out=notzr );
    Not(in=ng , out=notng );
    And(a=notzr , b=notng , out=cJGT );
     And(a=instruction[15] , b=instruction[3] , out=writeM );


    And(a=zr , b=instruction[1] , out=JEQ );    
    And(a=ng , b=instruction[2] , out=JLT );    
    And(a=cJGT , b=instruction[0] , out=JGT );
    Or(a=JEQ , b=JLT , out=JLE );    
    Or(a=JLE , b=JGT , out=jumptoA );   

    And(a=isCinstruction , b=jumptoA , out=loadPC ); 
    PC(in=ARegOut , load=loadPC , inc=true , reset=reset , out[0..14]=pc );


}
  
  